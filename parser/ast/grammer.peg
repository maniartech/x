{
  //
  // Inspired by pegjs arithmetic example:
  // https://github.com/pegjs/pegjs/blob/master/examples/arithmetics.pegjs
  //
  // Inspired from pegjs excel formula parser:
  // https://gist.github.com/gamerxl/cf5184a437b10d0f86197b102cf1255a
  //
  // JSON grammar:
  // https://github.com/mna/pigeon/blob/master/examples/json/json.peg
  //
  // Excel:
  // https://mrcsparker.wordpress.com/2015/07/26/excel-formula-grammar-for-peg-js/
  //
  // https://pkg.go.dev/github.com/mna/pigeon:
  //

  package ast

}

Input <- expr:Expr EOF {
    return expr, nil
}

Expr <- _ first:Term rest:( _ ( AddOp / CompOp ) _ Term )* _ {
    return eval(0, 0, first, rest), nil
}

Term <- first:Factor rest:( _ MulOp _ Factor )* {
    return eval(0, 0, first, rest), nil
}

Function <- fn:AlphaNum+ _ '(' _ args:Args? ')' {
    // fnName := toString(fn)
    // return getFunctionNode(0, 0, fnName, args.([]interface{})...), nil
    return nil, nil
}

Args <- first:Expr rest:( _ "," _ Expr )* {
    slice := toIfaceSlice(rest)
    if slice == nil {
        return []interface{}{first}, nil
    }

    args := make([]interface{}, len(slice)+1)
    args[0] = eval(0, 0, first, nil)
    for i, arg := range slice {
        args [i + 1] = eval(0, 0, arg, nil).([]interface{})[3]
    }
    return args, nil
}

Factor <- '(' expr:Expr ')' {
    return expr, nil
} / value:Value {
    return value, nil
}

AddOp <- ( "+" / "-" ) {
    return string(c.text), nil
}

MulOp <- ( "*" / "/" ) {
    return string(c.text), nil
}

CompOp <- ("=" / "<>" / "<=" / "<" / ">=" / ">") {
  return c.text, nil
}

Value <- val:( Object / Array / Number / String / Bool / Null / Function ) _ {
    return val, nil
}

Object <- '{' _ vals:( String _ ':' _ Expr ( ',' _ String _ ':' _ Expr )* )? '}' {
    res := make(map[string]interface{})
    valsSl := toIfaceSlice(vals)
    if len(valsSl) == 0 {
        return res, nil
    }
    res[valsSl[0].(string)] = valsSl[4]
    restSl := toIfaceSlice(valsSl[5])
    for _, v := range restSl {
        vSl := toIfaceSlice(v)
        res[vSl[2].(string)] = vSl[6]
    }
    return res, nil
}

Array <- '[' _ vals:( Expr ( ',' _ Expr )* )? ']' {
    valsSl := toIfaceSlice(vals)
    if len(valsSl) == 0 {
        return []interface{}{}, nil
    }
    res := []interface{}{valsSl[0]}
    restSl := toIfaceSlice(valsSl[1])
    for _, v := range restSl {
        vSl := toIfaceSlice(v)
        res = append(res, vSl[2])
    }
    return res, nil
}

///////////////////// Number /////////////////////

Number <- Integer ( '.' DecimalDigit+ )? Exponent? {
    // JSON numbers have the same syntax as Go, and are parseable using
    // strconv.ParseFloat
    value, err := strconv.ParseFloat(string(c.text), 64)
    return NewNumberNode(c.pos.line, c.pos.col, Number(value)), err
}

Integer <- [+-]? DecimalDigit+ {
    return strconv.Atoi(string(c.text))
}

Exponent <- 'e'i [+-]? DecimalDigit+

DecimalDigit <- [0-9]

AlphaNum <- [A-Za-z0-9]

/////////////////// Strings ////////////////

String <- DoubleQuoteString / SingleQuoteString

DoubleQuoteString <- '"' ( !EscapedChar . / '\\' EscapeSequence )* '"' {
    c.text = bytes.Replace(c.text, []byte(`\/`), []byte(`/`), -1)
    value, err := strconv.Unquote(string(c.text))
    return NewStringNode(c.pos.line, c.pos.col, value), err
}

SingleQuoteString <- "'" str:PlainString "'" {
    return NewStringNode(c.pos.line, c.pos.col, str.(string)), nil
}

PlainString <- ( !EscapedChar . / '\\' EscapeSequence )* {
    c.text = bytes.Replace(c.text, []byte(`\/`), []byte(`/`), -1)
    return NewStringNode(c.pos.line, c.pos.col, string(c.text)), nil
}

EscapedChar <- [\x00-\x1f"'\\]

EscapeSequence <- SingleCharEscape / UnicodeEscape

SingleCharEscape <- ["\\/bfnrt]

UnicodeEscape <- 'u' HexDigit HexDigit HexDigit HexDigit

HexDigit <- [0-9a-f]i

Bool <- "true" { return true, nil } / "false" { return false, nil }

Null <- "null" { return nil, nil }

_ "whitespace" <- [ \n\t\r]*

EOF <- !.
